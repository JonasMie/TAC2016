//noinspection GradleCompatible
apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "tac.android.de.truckcompanion"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    maven {
        url "https://jitpack.io"
    }
    maven {
        url "http://developer.skobbler.com/maven/"
    }

}

configurations {
    skobblersdk
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.google.android.gms:play-services-maps:8.4.0'
    compile 'com.android.support:design:23.4.0+'
    compile 'com.android.support:cardview-v7:23.4.0+'
    compile 'com.mcxiaoke.volley:library:1.0.19'
    compile 'com.github.JonasMie:MPAndroidChart:v2.2.4-alpha'
    skobblersdk "com.skobbler.ngx:SKMaps:2.5.0"
    compile files('libs/SKMaps.jar')
}


// Skobbler stuff setup
def assetsPath = "$projectDir/src/main/assets"
def libsPath = "$projectDir/libs"
def jniLibsPath = "$projectDir/src/main/jniLibs"

task installSKMaps << {
    copy {
        from configurations.skobblersdk
        into "$buildDir/skobblersdk-down"
        rename { String fileName -> 'skobblersdkres.zip' }
    }
    copy {
        from zipTree("$buildDir/skobblersdk-down/skobblersdkres.zip")
        into "$buildDir/skobblersdk-down"
    }
    delete("$jniLibsPath",
            "$assetsPath/SKMaps.zip",
            "$libsPath/SKMaps.jar")
    copy {
        from "${buildDir}/skobblersdk-down/jniLibs"
        into "$jniLibsPath"
    }
    copy {
        from "${buildDir}/skobblersdk-down/SKMaps.zip"
        into "$assetsPath"
    }
    copy {
        from "${buildDir}/skobblersdk-down/SKMaps.jar"
        into "$libsPath"
    }
    delete("$buildDir/skobblersdk-down")
    delete(configurations.skobblersdk)
}